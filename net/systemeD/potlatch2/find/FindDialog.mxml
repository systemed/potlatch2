<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	layout="vertical" showCloseButton="true"
	horizontalAlign="center" title="Search"
	width="458" height="350" verticalGap="0" creationComplete="txtFind.setFocus()">
	<fx:Script><![CDATA[
        import flash.events.Event;
		import mx.core.Application;
		import mx.core.FlexGlobals;
		import mx.events.CloseEvent;
		import mx.events.FlexEvent;
		import mx.managers.PopUpManager;
		import mx.rpc.events.ResultEvent;
		import mx.rpc.http.HTTPService;
		import mx.controls.Alert;
        import mx.events.ListEvent;
        		
		import net.systemeD.halcyon.Globals;
		
		private var nominatim:HTTPService = new HTTPService;
		private var nominatimURL:String = "http://open.mapquestapi.com/nominatim/v1/search?";	// ** FIXME - this should be user-configurable
		private var nomOptions:Object = { addressdetails:"1", limit:"10", format:"xml" };
        private var selectedResult:Object = {lat: "0", lon: "0"};

		[Bindable]
        private var searchResults:XMLList;
				
		
		public function init():void {
			PopUpManager.addPopUp(this, Application(FlexGlobals.topLevelApplication), true);
			PopUpManager.centerPopUp(this);
			this.addEventListener(CloseEvent.CLOSE, findDialog_close);
			
			var obj:SharedObject = SharedObject.getLocal("user_state","/");
	
		}
		
		private function findDialog_close(evt:CloseEvent):void {
			PopUpManager.removePopUp(this);
		}

		private function goFetch(q:String):void {

			var matches:Array=q.match(/^([\-\.\d]+)[\s,]+([\-\.\d]+)$/i);
			if (matches) {
				// lat/lon pair
				FlexGlobals.topLevelApplication.theMap.moveMapFromLatLon(Number(matches[1]), Number(matches[2]));
				PopUpManager.removePopUp(this);
				return;
			}

			var searchCall:String = nominatimURL;
			for (var item:String in nomOptions) {
				searchCall += item + "="; 
				searchCall += nomOptions[item] + "&";
			}
			searchCall += "q=" + q;
			this.nominatim.url = searchCall
			this.nominatim.addEventListener(ResultEvent.RESULT, goneFetched);
			this.nominatim.resultFormat = 'e4x';

			this.nominatim.send();
			
		}
		
		private function goneFetched(e:ResultEvent):void {
			searchResults = e.result.* as XMLList;
		}

		protected function btnFind_clickHandler(event:MouseEvent):void {
			goFetch(txtFind.text);
		}


		protected function txtFind_enterHandler(event:FlexEvent):void {
			goFetch(txtFind.text);
		}

        private function setSelected(evt:ListEvent):void {
            //Alert.show(evt.itemRenderer.data.@lat);
            selectedResult = { lat: evt.itemRenderer.data.@lat, lon: evt.itemRenderer.data.@lon };
        }
        
        private function goToSelected():void {
            FlexGlobals.topLevelApplication.theMap.moveMapFromLatLon(selectedResult.lat, selectedResult.lon);
            //FlexGlobals.topLevelApplication.theMap.redraw();
            PopUpManager.removePopUp(this);
        }
        
        
	]]>
	</fx:Script>

	<mx:HBox horizontalAlign="left" width="100%">
		<mx:Label text="Enter place name: " />
		<mx:TextInput id="txtFind" width="100%" enter="txtFind_enterHandler(event)"/>
		<mx:Button label="Search" id="btnFind" click="btnFind_clickHandler(event)"/>		
	</mx:HBox>
	
	<mx:Spacer height="10"/>
	
	<mx:DataGrid id="dgResults" dataProvider="{searchResults}" change="setSelected(event)" width="100%" height="100%">
		<mx:columns>
			<mx:DataGridColumn headerText="Name" dataField="@display_name" minWidth="200" />
			<mx:DataGridColumn headerText="Type" dataField="@type"/>
			<mx:DataGridColumn headerText="Class" dataField="@class"/>            
			<mx:DataGridColumn headerText="ID" dataField="@osm_id"/>
        </mx:columns>
	</mx:DataGrid>

    <mx:ControlBar>
    	<mx:Label text="Results from Nominatim" />
        <mx:Spacer width="100%"/>
        <mx:Button label="Cancel" click="PopUpManager.removePopUp(this);" styleName="titleWindowButton" />
        <mx:Button label="Jump to" click="goToSelected();" styleName="titleWindowButton" enabled="{dgResults.selectedItem != null? true : false}" />        
    </mx:ControlBar>
    
</mx:TitleWindow>
